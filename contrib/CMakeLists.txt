add_subdirectory(coro-http EXCLUDE_FROM_ALL)

include(FindPkgConfig)

if(NOT USE_BUNDLED_DEPENDENCIES)
    find_package(nlohmann_json 3.9.1 REQUIRED)
    set_target_properties(nlohmann_json::nlohmann_json PROPERTIES IMPORTED_GLOBAL TRUE)

    find_package(fmt 8.0.0 REQUIRED)
    set_target_properties(fmt::fmt PROPERTIES IMPORTED_GLOBAL TRUE)
    add_library(fmt ALIAS fmt::fmt)

    find_package(pugixml 1.11 REQUIRED)
    set_target_properties(pugixml::pugixml PROPERTIES IMPORTED_GLOBAL TRUE)

    if(PKG_CONFIG_FOUND)
        pkg_check_modules(avcodec REQUIRED libavcodec>=58.134.100 IMPORTED_TARGET GLOBAL)
        pkg_check_modules(avformat REQUIRED libavformat>=58.76.100 IMPORTED_TARGET GLOBAL)
        pkg_check_modules(avutil REQUIRED libavutil>=56.70.100 IMPORTED_TARGET GLOBAL)
        pkg_check_modules(avfilter REQUIRED libavfilter>=7.110.100 IMPORTED_TARGET GLOBAL)
        pkg_check_modules(swscale REQUIRED libswscale>=5.9.100 IMPORTED_TARGET GLOBAL)
        pkg_check_modules(swresample REQUIRED libswresample>=3.9.100 IMPORTED_TARGET GLOBAL)
        add_library(avcodec ALIAS PkgConfig::avcodec)
        add_library(avformat ALIAS PkgConfig::avformat)
        add_library(avutil ALIAS PkgConfig::avutil)
        add_library(avfilter ALIAS PkgConfig::avfilter)
        add_library(swscale ALIAS PkgConfig::swscale)
        add_library(swresample ALIAS PkgConfig::swresample)
    else()
        find_package(FFMPEG 4.4 REQUIRED)
        add_library(avcodec INTERFACE)
        target_include_directories(avcodec INTERFACE "${FFMPEG_libavcodec_INCLUDE_DIRS}")
        target_link_libraries(avcodec INTERFACE ${FFMPEG_libavcodec_LIBRARY})
        add_library(avformat INTERFACE)
        target_include_directories(avformat INTERFACE "${FFMPEG_libavformat_INCLUDE_DIRS}")
        target_link_libraries(avformat INTERFACE ${FFMPEG_libavformat_LIBRARY})
        add_library(avutil INTERFACE)
        target_include_directories(avutil INTERFACE "${FFMPEG_libavutil_INCLUDE_DIRS}")
        target_link_libraries(avutil INTERFACE ${FFMPEG_libavutil_LIBRARY})
        add_library(avfilter INTERFACE)
        target_include_directories(avfilter INTERFACE "${FFMPEG_libavfilter_INCLUDE_DIRS}")
        target_link_libraries(avfilter INTERFACE ${FFMPEG_libavfilter_LIBRARY})
        add_library(swscale INTERFACE)
        target_include_directories(swscale INTERFACE "${FFMPEG_libswscale_INCLUDE_DIRS}")
        target_link_libraries(swscale INTERFACE ${FFMPEG_libswscale_LIBRARY})
        add_library(swresample INTERFACE)
        target_include_directories(swresample INTERFACE "${FFMPEG_libswresample_INCLUDE_DIRS}")
        target_link_libraries(swresample INTERFACE ${FFMPEG_libswresample_LIBRARY})
    endif()

    find_path(CRYPTOPP_INCLUDE cryptopp/config.h REQUIRED)
    find_library(CRYPTOPP_LIBRARY cryptopp cryptopp-static REQUIRED)
    add_library(cryptopp UNKNOWN IMPORTED GLOBAL)
    set_target_properties(cryptopp
        PROPERTIES
            IMPORTED_LOCATION "${CRYPTOPP_LIBRARY}"
            INTERFACE_INCLUDE_DIRECTORIES "${CRYPTOPP_INCLUDE}"
    )
else()
    add_subdirectory(json EXCLUDE_FROM_ALL)
    add_subdirectory(fmt EXCLUDE_FROM_ALL)
    add_subdirectory(pugixml EXCLUDE_FROM_ALL)

    set(ZLIB_COMPAT 1 CACHE BOOL "")
    add_subdirectory(zlib-ng EXCLUDE_FROM_ALL)

    set(FFMPEG_SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/ffmpeg")
    add_subdirectory(ffmpeg-config EXCLUDE_FROM_ALL)

    set(BUILD_SHARED 0 CACHE BOOL "")
    set(BUILD_STATIC 1 CACHE BOOL "")
    set(BUILD_TESTING 0 CACHE BOOL "" FORCE)
    set(USE_INTERMEDIATE_OBJECTS_TARGET OFF CACHE BOOL "")
    if(MSVC AND CMAKE_CXX_COMPILER_ID MATCHES "Clang")
        set(DISABLE_ASM ON CACHE BOOL "")
    endif()
    add_subdirectory(cryptopp EXCLUDE_FROM_ALL)
    if(MSVC AND CMAKE_CXX_COMPILER_ID MATCHES "Clang")
        target_compile_definitions(cryptopp-static PUBLIC __EXCEPTIONS CRYPTOPP_SUPPORT_CLANG_CL)
    endif()
    add_library(cryptopp ALIAS cryptopp-static)
    if(ANDROID)
        target_include_directories(cryptopp-static PRIVATE ${CMAKE_ANDROID_NDK}/sources/android/cpufeatures)
    endif()
endif()